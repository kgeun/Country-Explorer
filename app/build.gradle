plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.kgeun.countryexplorer"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testApplicationId "com.kgeun.countryexplorer.test"
        testInstrumentationRunner "com.kgeun.countryexplorer.CETestRunner"
    }

    signingConfigs {
        releaseWithSignedKey {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        debugWithDebugKey {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseWithSignedKey
            debuggable false
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debugWithDebugKey
            debuggable true
        }
        applicationVariants.all { variant ->
            appendVersionName(variant, defaultConfig)
        }
    }

    flavorDimensions "default"
    productFlavors {
        live {
            dimension "default"
            manifestPlaceholders = [appLabel: "@string/app_name"]
        }
        dev {
            dimension "default"
            manifestPlaceholders = [appLabel: "@string/app_name_dev"]
            applicationIdSuffix '.dev'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

def appendVersionName(variant, defaultConfig) {
    variant.outputs.all { output ->
        def date = new Date()
        def buildDate = date.format('yyyyMMdd')
        def buildType = variant.buildType.name
        def flavor = variant.productFlavors[0].name

        outputFileName = "CountryExplorer_" + flavor + "_" + buildType + "_" + defaultConfig.versionName + "_" + defaultConfig.versionCode + "_" + buildDate + ".apk"
    }
}

kapt {
    correctErrorTypes true
}


dependencies {
    // Firebase Libraries
    implementation platform('com.google.firebase:firebase-bom:28.4.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-perf'

    // Kotlin Library
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'

    // Lifecycle Libraries
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"

    // Layout Components
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.activity:activity-ktx:1.3.0'

    // Navigation Components
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation "androidx.navigation:navigation-dynamic-features-fragment:2.3.5"
    debugImplementation "androidx.fragment:fragment-testing:1.3.6"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // SVG Loader
    implementation 'com.github.corouteam:GlideToVectorYou:v2.0.0'

    // Hilt Dependency Injection Library
    implementation "com.google.dagger:hilt-android:2.38.1"
    kapt "com.google.dagger:hilt-android-compiler:2.38.1"
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // Hilt Testing
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.38.1'
    kaptAndroidTest 'com.google.dagger:hilt-android-testing:2.38.1'
    testImplementation 'com.google.dagger:hilt-android-testing:2.38.1'

    kaptTest 'com.google.dagger:hilt-android-compiler:2.38.1'
    kaptTest 'com.google.dagger:hilt-compiler:2.38.1'
    testAnnotationProcessor 'com.google.dagger:hilt-android-compiler:2.38.1'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.38.1'

    // Room
    implementation "androidx.room:room-runtime:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"
    implementation "androidx.room:room-ktx:2.3.0"
    testImplementation "androidx.room:room-testing:2.3.0"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.12.0"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    // Loading Image Library
    implementation 'com.victor:lib:1.0.4'

    // Test Libraries
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.3.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    testImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.0.1'
}